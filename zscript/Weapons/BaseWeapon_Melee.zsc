extend class PB_WeaponBase
{
    States
    {
		/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		// Glory Kill
		////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
			
		DoFatality:
			TNT1 A 0 A_JumpIfInventory("GoFatality",1,"DoingFatality");
			TNT1 A 0 A_Takeinventory("Fear",1);
			TNT1 A 0
			{
				if(GetCvar("be_Protection") == 1)
				{
					A_Giveinventory("Superarmor",1);
				}
			}
			"####" "#" 0 { return ResolveState("Steady"); }
			
		DoingFatality:
			TNT1 A 1 SetPlayerProperty(0,1,0);
			TNT1 A 0
			{
				if(GetCvar("be_Protection") == 1)
				{
					A_Giveinventory("Superarmor",1);
				}
			}
			"####" "#" 0 { return ResolveState("DoFatality"); }
		
		PerformExecution:
			TNT1 A 0
			{
				A_StartSound("GloryKill", CHAN_UI);
				if(GetCvar("be_Protection") == 1)
				{
					A_Giveinventory("Superarmor",1);
				}
				if(GetCvar("be_Fear") == 1)
				{
					A_Giveinventory("Fear",1);
				}
			}
			#### AAA 0 PB_ExecuteGK(); // Check if monster can be executed, jump to execution handler instead
			TNT1 A 0;
			"####" "#" 0 { return ResolveState("DoFatality"); }
		
		//Unused	old code
		PerformGloryKill:
			TNT1 A 0
			{
				A_StartSound("GloryKill", CHAN_UI);
				if(GetCvar("be_Protection") == 1)
				{
					A_Giveinventory("Superarmor",1);
				}
				if(GetCvar("be_Fear") == 1)
				{
					A_Giveinventory("Fear",1);
				}
			}
			TNT1 A 0 A_CustomPunch(10000,1,0,"GloryKillPuff",150,0,0);
			TNT1 A 0;
			"####" "#" 0 { return ResolveState("DoFatality"); }
		
		/////////////////////////////////////////////////////////////////////////////////////////////
		// Glory Melee
		////////////////////////////////////////////////////////////////////////////////////////////
		
		GloryMelee:
			SAWG G 0
			{	
				if(CountInv("CrucibleEnergy") > 0)
				{
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_TakeInventory("ADSmode",1);
					A_StopSound(1);
					A_StopSound(6);
					return ResolveState("Crucible");
				}
					A_Print("Not Enough Energy");
					A_StartSound("Beep");
					A_TakeInventory("DoGloryMelee",1);
					return ResolveState(null);
			}
			Goto GoingToReady;
			
		
		/////////////////////////////////////////////////////////////////////////////////////////////
		// Blood Punch
		////////////////////////////////////////////////////////////////////////////////////////////
		
		BloodPunch:
			SCL0 A 0 A_ClearOverlays(-5, 80);
			SCL0 A 0 A_GunFlash("Null");
			SCL0 A 0 A_StopSound(1);
			SCL0 A 0 A_StopSound(6);
			P1NK ABCD 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
				A_SetRoll(roll+2);
			}
			TNT1 A 1 A_SetRoll(roll+2);
			TNT1 A 2;
			TNT1 A 0 A_StartSound("weapons/ultrwhoosh", 5);
			P1NK EF 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
				A_SetRoll(roll-2);
			}
			TNT1 A 0 {
					A_Saw("", "player/cyborg/fist", 120, "BloodPunchPuff", SF_NOPULLIN | SF_NOTURN , 128);
			}
			P1NK G 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
			}
			P1NK HIJKLMNO 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
				A_SetRoll(roll-1);
				}
			P1NK O 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
			}
			P1NK PQ 1 {
				if (CountInv("PowerBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P2NK");}
				if (CountInv("PowerBlueBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P3NK");}
				if (CountInv("PowerGreenBloodOnVisor") >= 1 ) { A_SetWeaponSprite("P4NK");}
				A_SetRoll(roll+2);
			}
			TNT1 A 0 A_SetRoll(0);
			TNT1 A 0 {
				if(CountInv("BloodPunchKilled"))
				{
					A_StartSound("BloodPunch", CHAN_AUTO);
					A_TakeInventory("BloodPunchKilled");
					A_TakeInventory("BPtoken");
					A_Explode(125, 200, 0, true, 200 ,0 ,0, "BPImpactPuff", "BloodPunch");
				}
			}
			TNT1 A 0 {PB_SetUsingMelee(false);}
			TNT1 A 0 A_TakeInventory("DoGloryMelee", 1);
			TNT1 A 1 A_Raise;
			Goto GoingToReady;
		
		
		/////////////////////////////////////////////////////////////////////////////////////////////
		// Crucible
		////////////////////////////////////////////////////////////////////////////////////////////
			
		Crucible:
			SCL0 A 0 A_ClearOverlays(-5, 80);
			SCL0 A 0 A_GunFlash("Null");
			CRCB A 1 A_StartSound("Crucible_Select", CHAN_WEAPON, CHANF_OVERLAP);
			CRCB AABBCCJJ 2 bright A_WeaponOffset(-1, 2, WOF_ADD);
			CRCB D 1 bright
			{
				A_StartSound("Crucible_Deploy", CHAN_WEAPON, CHANF_OVERLAP);
				A_WeaponOffset(-2, 4, WOF_ADD);
				A_TakeInventory("CrucibleEnergy",1);
			}
			CRCB D 3 bright A_WeaponOffset(-2, 4, WOF_ADD);//-10, 20
			CRCB DDD 1 bright A_WeaponOffset(3, -6, WOF_ADD);
			CRCB D 1 bright A_WeaponOffset(0.5, 4, WOF_ADD);
			Goto HoldCrucible;
			
		HoldCrucible:	
			CRCB D 1 bright A_StartSound("Crucible_Idle",10,CHANF_LOOP|CHANF_OVERLAP,0.2);
			CRCB D 1 bright
			{
				A_WeaponReady();
				if (!countinv("DoGloryMelee"))
				{
					return ResolveState("DoCrucible");
				}
				return ResolveState(null);
			}
			Goto HoldCrucible+1;
			
		DoCrucible:
			CRCB D 1 
			{
				A_StartSound("Crucible_Swing", CHAN_WEAPON, CHANF_OVERLAP);
				A_WeaponOffset(10,2, WOF_ADD);
			}
			CRCB D 1 A_WeaponOffset(20, 4, WOF_ADD);
			CRCB D 1 A_WeaponOffset(30, 6, WOF_ADD);
			CRCB D 1 A_WeaponOffset(40, 8, WOF_ADD);
			CRCB D 1 A_WeaponOffset(50,10, WOF_ADD);
			CRCB D 1 A_WeaponOffset(60,12, WOF_ADD);
			TNT1 A 0 A_WeaponReady(WRF_NoFire|WRF_NoSwitch);
			
			//First Swing
			
			CRCB EEF 1 
			{
				A_Saw("", "Crucible_Slice", 300, "CruciblePuff", SF_NOPULLIN | SF_NOTURN , 150, 32);
			}
			CRCB G 1
			{
				A_WeaponOffset(-40, 0, WOF_ADD);
				A_Saw("", "Crucible_Slice", 300, "CruciblePuff", SF_NOPULLIN | SF_NOTURN , 150, 32);
				A_SpawnItemEx("CrucibleBladeWave", 0, 0, Height / 2, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_TRANSFERPITCH);
			}
			
			TNT1 A 0 A_WeaponReady(WRF_NoFire|WRF_NoSwitch);
			
			CRCB HII 1 
			{
				A_WeaponOffset(-40, 8, WOF_ADD);
				A_Saw("", "Crucible_Slice", 300, "CruciblePuff", SF_NOPULLIN | SF_NOTURN , 150, 32);
			}
			CRCB D 0 A_WeaponOffset(325, 24, WOF_ADD);
			
			NULL A 10;
			
			TNT1 A 0
			{
				A_TakeInventory("DoGloryMelee",1);
			}
			
			CRCB D 1 A_WeaponOffset(-60, -12, WOF_ADD);
			CRCB D 1 A_WeaponOffset(-50, -10, WOF_ADD);
			CRCB D 1 A_WeaponOffset(-40, -8, WOF_ADD);
			CRCB D 1 A_WeaponOffset(-30, -6, WOF_ADD);
			CRCB D 1 A_WeaponOffset(-20, -4, WOF_ADD);
			
			CRCB D 1 bright A_WeaponOffset(-0.5, -4, WOF_ADD);
			CRCB DDD 1 bright A_WeaponOffset(-3, 6, WOF_ADD);
			CRCB D 3 bright A_WeaponOffset(2, -4, WOF_ADD);
			
			CRCB D 1 bright A_WeaponOffset(2, -4, WOF_ADD);
			
			CRCB JJCCBBAA 2 bright A_WeaponOffset(1, -2, WOF_ADD);
			CRCB A 1 A_StopSound(10);
			CRCB A 1 A_StartSound("Crucible_Deselect", CHAN_WEAPON, CHANF_OVERLAP);
			CRCB A 5;
			TNT1 A 1 A_Raise;
			Goto GoingToReady;
			
		/////////////////////////////////////////////////////////////////////////////////////////////
		// Shoulder Cannon
		////////////////////////////////////////////////////////////////////////////////////////////
			
		FireShoulderCannon:
			TNT1 A 0
			{	
				if(CountInv("FlameBelchSelected") > 0)
				{
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_TakeInventory("ADSmode",1);
					A_StopSound(CHAN_WEAPON);
					A_StopSound(CHAN_6);
					return ResolveState("FireFlameBelch");
				}
				if(CountInv("IceBombSelected") > 0)
				{
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_TakeInventory("ADSmode",1);
					A_StopSound(CHAN_WEAPON);
					A_StopSound(CHAN_6);
					return ResolveState("FireIceBomb");
				}
				return ResolveState(null);
			}
			TNT1 A 0 A_StartSound("Equip_notready");
			TNT1 A 0 A_TakeInventory("DoShoulderCannon", 1);
			Goto GoingToReady;
			
		FireFlameBelch:
			TNT1 A 0
			{
				if (countinv("PB_Fuel") < 50)
				{
					return ResolveState("EmptyCannon");
				}
				A_TakeInventory("Zoomed",1);
				A_ZoomFactor(1.0);
				A_TakeInventory("ADSmode",1);
				return ResolveState(null);
			}
			TNT1 A 0
			{
				if(GetCvar("be_FBperformance"))
				{
					return ResolveState("FireFlameBelchSimple");
				}
				return ResolveState(null);
			}
			SCL0 A 0 A_ClearOverlays(-5, 80);
			SCL0 A 0 A_GunFlash("Null");
			SCL0 ABCD 1 A_StartSound("Flamethrower/Charge",1);
			SCL0 EFGHIJ 1;
			SCL0 K 1 A_Quake(8, 8, 0, 32);
			TNT1 A 0 A_Recoil (4);
			TNT1 A 0 A_StartSound("Flamebelch", 1);
			SCF0 A 5 bright A_GunFlash;
			TNT1 A 0 A_AlertMonsters;
			TNT1 A 0 A_TakeInventory("PB_Fuel",50);
			SCF0 BCDBCDBCD 1 bright {
				A_FireProjectile("SCFireMissile", -3, 0, Random(-25, -20), 12, FPF_NOAUTOAIM, 0);
			}
			SCF0 EF 1;
			SCL0 KJIHGFEDCBA 1;
			TNT1 A 0 A_TakeInventory("DoShoulderCannon", 1);
			TNT1 A 1 A_Raise;
			Goto GoingToReady;
		
		FireFlameBelchSimple:
			TNT1 A 0
			{
				if (countinv("PB_Fuel") < 50)
				{
					return ResolveState("EmptyCannon");
				}
				A_TakeInventory("Zoomed",1);
				A_ZoomFactor(1.0);
				A_TakeInventory("ADSmode",1);
				return ResolveState(null);
			}
			SCL0 A 0 A_ClearOverlays(-5, 80);
			SCL0 A 0 A_GunFlash("Null");
			SCL0 ABCD 1 A_StartSound("Flamethrower/Charge",1);
			SCL0 EFGHIJ 1;
			SCL0 K 1 A_Quake(8, 8, 0, 32);
			TNT1 A 0 A_Recoil (4);
			TNT1 A 0 A_StartSound("Flamebelch", 1);
			SCF0 A 5 bright A_GunFlash;
			TNT1 A 0 A_AlertMonsters;
			TNT1 A 0 A_TakeInventory("PB_Fuel",50);
			SCF0 BCDBCDBCD 1 bright {
				A_FireProjectile("SCFireMissileSimple", -3, 0, Random(-25, -20), 12, FPF_NOAUTOAIM, 0);
			}
			SCF0 EF 1;
			SCL0 KJIHGFEDCBA 1;
			TNT1 A 0 A_TakeInventory("DoShoulderCannon", 1);
			TNT1 A 1 A_Raise;
			Goto GoingToReady;
		
		FireIceBomb:
			TNT1 A 0 {
				if (countinv("PB_RocketAmmo") < 1)
				{
					return ResolveState("EmptyCannon");
				}
				A_TakeInventory("Zoomed",1);
				A_ZoomFactor(1.0);
				A_TakeInventory("ADSmode",1);
				return ResolveState(null);
			}
			SCL0 A 0 A_ClearOverlays(-5, 80);
			SCL0 A 0 A_GunFlash("Null");
			SCL0 ABCD 1 A_StartSound("Flamethrower/Charge",1);
			SCL0 EFGHIJ 1;
			SCL0 K 1 A_Quake(8, 8, 0, 32);
			TNT1 A 0 A_Recoil (4);
			SCF0 A 3 bright A_GunFlash;
			TNT1 A 0 A_AlertMonsters;
			TNT1 A 0 A_TakeInventory("PB_RocketAmmo",1);
			SCF0 BC 1 bright;
			SCFG D 1 bright {
				A_StartSound ("weapons/firegrenade", CHAN_WEAPON);
				A_FireProjectile("SC_CryoGrenade", -1, 0, Random(-22, -18), 12, FPF_NOAUTOAIM, 0);
			}
			SCF0 EF 1;
			SCL0 KJIHGFEDCBA 1;
			TNT1 A 0 A_TakeInventory("DoShoulderCannon", 1);
			TNT1 A 1 A_Raise;
			Goto GoingToReady;
			
		EmptyCannon:
			TNT1 A 0 A_StopSound(CHAN_WEAPON);
			TNT1 A 0 A_StartSound("Equip_notready");
			TNT1 A 0 A_TakeInventory("DoShoulderCannon", 1);
			Goto GoingToReady;
			
		/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		// Glory Kill End
		/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////	
	
        // The quick melee is going to be reworked, knife combos are tacky and bloat the code
		// The quick melee should have a primary purpose of executing enemies or finishing them off to the point where they can be executed
		QuickMelee:
			TNT1 A 0 {
				A_ClearOverlays(-10,65);
				A_Gunflash("Null");
			}
			TNT1 A 0
			{
				if(countinv("FinisherToken",AAPTR_PLAYER_GETTARGET)>=1 && A_CheckLOF("Null",CLOFF_SETTARGET|CLOFF_NOAIM_VERT|CLOFF_IGNOREGHOST|CLOFF_MUSTBESOLID ,180))
				{
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_TakeInventory("ADSmode",1);
					return ResolveState("PerformExecution");
				}
				if(countinv("BPToken") == 5){
					A_TakeInventory("Zoomed",1);
					A_ZoomFactor(1.0);
					A_TakeInventory("ADSmode",1);
					A_TakeInventory("BloodPunchKilled");
					return ResolveState("BloodPunch");
				}
				return ResolveState(null);
			}
			#### A 0 {
				A_StopSound(CHAN_WEAPON);
				A_StopSound(CHAN_VOICE);
				A_StopSound(CHAN_6);
				A_StopSound(CHAN_7);
			}
			TNT1 A 0 A_JumpIfInventory("CantDoAction",1,"FailOverlay");
			TNT1 A 0 A_JumpIfHealthLower(0, "FailOverlay");
		GoMeleeInstead:
			TNT1 A 0 {
				A_Overlay(PSP_FLASH, "FlashPunching");
				A_GiveInventory("HasCutingWeapon", 1);
				A_TakeInventory("Zoomed",1);
				A_ZoomFactor(1.0);
				A_TakeInventory("ADSmode",1);
				A_StartSound("KNIFSWNG", 1);
				A_Overlay(-10, "FirstPersonLegsStand");
			}
			MC3S AB 1 {
			//	if(JustPressed(BT_USER2)) return PB_Execute();
				return ResolveState(null);
			}
			MC3S C 1 {
			//	if(JustPressed(BT_USER2)) return PB_Execute();
				return ResolveState(null);
			}
			MC3S D 1 {
			//	if(JustPressed(BT_USER2)) return PB_Execute();
				return ResolveState(null);
			}
			TNT1 A 0 {
			if(CountInv("PB_PowerStrength") == 1) A_FireProjectile("SuperKnifeSwing",0,0,0,0,0,0);
			else A_FireProjectile("KnifeSwing",0,0,0,0,0,0);
			}
			MC3S EFGHIJ 1 {
			//	if(JustPressed(BT_USER2)) return PB_Execute();
				return ResolveState(null);
			}
			TNT1 AAAA 1 {
			//	if(JustPressed(BT_USER2)) return PB_Execute();
				return ResolveState(null);
			}
			TNT1 A 0 {
				A_TakeInventory("KnifeHasHit",1);
				A_TakeInventory("HasCutingWeapon", 1);
				PB_SetUsingMelee(false);
			}
			TNT1 A 0 A_JumpIfInventory("GrabbedBarrel", 1, "IdleBarrel");
			TNT1 A 0 A_JumpIfInventory("GrabbedFlameBarrel", 1, "IdleFlameBarrel");
			TNT1 A 0 A_JumpIfInventory("GrabbedIceBarrel", 1, "IdleIceBarrel");
			Goto Ready3;
		//Kicks
		DoKick:
			TNT1 A 0 A_JumpIfInventory("PlayerIsDead",1,"FailOverlay");
			TNT1 A 0 A_JumpIfHealthLower(0, "FailOverlay");
			TNT1 A 0 {
				A_Overlay(-10, "FirstPersonLegsKickRight");
				A_TakeInventory("Zoomed",1);
				A_TakeInventory("ADSmode",1);
				A_ZoomFactor(1.0);
				A_GiveInventory("Kicking",1);
				}
			TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "Steady");
			TNT1 A 0 A_JumpIfInventory("CanSlideKick", 1, "SlideKick");
			TNT1 A 0 A_JumpIf (vel.Z != 0, "AirKick");
			TNT1 A 0 A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, true);
			KIC1 A 1 {
				A_StartSound("KICK",0);
				SetPlayerProperty(0,1,0);
				if(CountInv("GrabbedBarrel") == 1 || CountInv("GrabbedIceBarrel") == 1 || CountInv("GrabbedFlameBarrel") == 1)
					A_Overlay(PSP_WEAPON, "FlashBarrelKicking");
				else if(PB_WeaponIsInReadyState())
				{
					A_Overlay(PSP_WEAPON, "FlashKicking");
					A_OverlayOffset(PSP_WEAPON, 0, 32);
					A_GunFlash("Null");
				}
				A_SetAngle(-0.5 + angle, SPF_INTERPOLATE);
				A_SetPitch(+0.5 + pitch, SPF_INTERPOLATE);
				A_SetRoll(roll-2);
				A_ZoomFactor(0.98);
				}
			KIC1 B 1 {
				A_SetAngle(-0.5 + angle, SPF_INTERPOLATE);
				A_SetPitch(+0.5 + pitch, SPF_INTERPOLATE);
				A_SetRoll(roll-2);
				A_ZoomFactor(0.970);
				}
			KIC1 C 1 {
				A_SetAngle(-0.5 + angle, SPF_INTERPOLATE);
				A_SetPitch(+0.5 + pitch, SPF_INTERPOLATE);
				A_SetRoll(roll-2);
				A_ZoomFactor(0.965);
			}
			KIC1 D 1 { 
				A_SetPitch(+1.25 + pitch, SPF_INTERPOLATE);
				A_ZoomFactor(0.950);
				}
			KIC1 E 1 A_Recoil(-1);
			KIC1 F 1 { //No more unneeded Super states here, this now unified into a single state with an if else statement.
				if(CountInv("PB_PowerStrength") == 1) A_FireProjectile("SuperKickAttack", 0, 0, 0, -7);
				else A_FireProjectile("KickAttack", 0, 0, 0, -7);
			}
			KIC1 GG 1;
			KIC1 I 1 {
				A_ZoomFactor(0.965);
				A_SetAngle(+1 + angle, SPF_INTERPOLATE);
				A_TakeInventory("Kicking",1);
				}
			KIC1 H 1 {
				A_SetPitch(-0.5 + pitch, SPF_INTERPOLATE);
				A_SetAngle(+1 + angle, SPF_INTERPOLATE);
				A_SetRoll(roll+2);
				A_ZoomFactor(0.970);
			}
			KIC1 J 1 {
				A_SetPitch(-0.5 + pitch, SPF_INTERPOLATE);
				A_SetRoll(roll+2);
				A_SetAngle(+0.5 + angle, SPF_INTERPOLATE);
				A_ZoomFactor(0.98);
				}
			KIC1 K 1 {
				A_SetPitch(-0.5 + pitch, SPF_INTERPOLATE);
				A_SetRoll(roll+2);
				A_SetAngle(+0.5 + angle, SPF_INTERPOLATE);
				A_ZoomFactor(0.99);
			}
			KIC1 L 1 {
				A_SetAngle(+0.5 + angle, SPF_INTERPOLATE);
				A_SetPitch(-0.5 + pitch, SPF_INTERPOLATE);
				A_ZoomFactor(1.0);
				}
			KIC1 A 1 {
				A_SetAngle(-0.5 + angle, SPF_INTERPOLATE);
				A_SetPitch(-1.25 + pitch, SPF_INTERPOLATE);
				SetPlayerProperty(0,0,0);
				PB_SetUsingKick(false);
			}
			TNT1 A 0 {
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, false);
				A_Overlay(-10, "FirstPersonLegsStand");
			}
			Stop;
		FailOverlay:
			TNT1 A 0 {
				A_StopSound(CHAN_WEAPON);
				A_StopSound(CHAN_VOICE);
				A_StopSound(CHAN_6);
				A_StopSound(CHAN_7);
			}
			TNT1 A 0 A_JumpIfInventory("CantDoAction",1,1);
			Goto StopFail;
			TNT1 A 0 A_JumpIf(invoker.Health > 0, 2);
			TNT1 A 1 {
				PB_SetUsingKick(false);
				PB_SetUsingMelee(false);
				PB_SetUsingEquipment(false);
				PB_SetExecutingEnemy(false);
				}
			Loop;
		StopFail:
			TNT1 A 1 A_SetInventory("CantDoAction",0);
			Stop;
		SlideKick:
			TNT1 A 0 {
				A_GiveInventory("KeepLaserDeactivated",1);
				A_GiveInventory("IsSlideKicking", 1);
				if(CountInv("GrabbedBarrel") == 1 || CountInv("GrabbedIceBarrel") == 1 || CountInv("GrabbedFlameBarrel") == 1)
					A_Overlay(PSP_WEAPON, "FlashBarrelSlideKicking");
				else if(PB_WeaponIsInReadyState())
				{
					A_Overlay(PSP_WEAPON, "FlashSlideKicking");
					A_GunFlash("Null");
					A_OverlayOffset(PSP_WEAPON, 0, 32);
				}
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, true);
				}
			SLDK A 1 A_ZoomFactor(0.98);
			SLDK B 1 A_ZoomFactor(0.96);
			SLDK C 1 A_ZoomFactor(0.94);
			SLDK D 1 A_ZoomFactor(0.92);
			SLDK EF 1;
			TNT1 A 0 A_JumpIf(Height > 54, "LowerKick");
			TNT1 A 0 A_JumpIfInventory("KickHasHitAnObject", 1, "LowerKick");
			SLDK GHIJKLMNMLKJ 1 {
				if(CountInv("PB_PowerStrength") == 1) A_FireProjectile("SuperKickAttack", 0, 0, 0, -31);
				else A_FireProjectile("KickAttack", 0, 0, 0, -31);
				if(CountInv("KickHasHitAnObject") == 1) return ResolveState("LowerKick");
				if(Height > 50) return ResolveState("LowerKick");
				return ResolveState(null);
			}
		LowerKick:
			TNT1 A 0 {
				if(CountInv("GrabbedBarrel") == 1 || CountInv("GrabbedIceBarrel") == 1 || CountInv("GrabbedFlameBarrel") == 1)
					A_Overlay(PSP_WEAPON, "FlashBarrelSlideKickingStop");
				else if(PB_WeaponIsInReadyState())
				{
					A_Overlay(PSP_WEAPON, "FlashSlideKickingStop");
					A_GunFlash("Null");
					A_OverlayOffset(PSP_WEAPON, 0, 32);
				}
				A_ZoomFactor(1.0);
				A_StartSound("SlideKick/End",0);
				A_Takeinventory("Kicking",1);
				A_Takeinventory("KickHasHitAnObject",1);
				A_TakeInventory("IsSlideKicking", 1);
				A_Takeinventory("PB_LockScreenTilt",1);
				PB_SetUsingKick(false);
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, true);
			}
			SLDK OPQRSTU 1;
			TNT1 A 0 {
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, false);
				A_Overlay(-10, "FirstPersonLegsStand");
			}
			Stop;
		AirKick:
			TNT1 A 0 {
				A_StartSound("KICK", 6);
				A_Recoil (-6);
				if(CountInv("GrabbedBarrel") == 1 || CountInv("GrabbedIceBarrel") == 1 || CountInv("GrabbedFlameBarrel") == 1)
					A_Overlay(PSP_WEAPON, "FlashBarrelAirKicking");
				else if(PB_WeaponIsInReadyState())
				{
					A_Overlay(PSP_WEAPON, "FlashAirKicking");
					A_GunFlash("Null");
					A_OverlayOffset(PSP_WEAPON, 0, 32);
				}
				A_OverlayOffset(PSP_WEAPON, 0, 32);
				A_ZoomFactor(0.95);
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, true);
			}
			AKCK ABC 1 A_SetRoll(roll-2, SPF_INTERPOLATE);
			AKCK DEFGH 1;
			RIFF A 0 {
				if(CountInv("PB_PowerStrength") == 1) A_FireProjectile("SuperAirKickAttack", 0, 0, 0, -31);
				else A_FireProjectile("AirKickAttack", 0, 0, 0, -31);
				A_ZoomFactor(0.975);
			}
			AKCK IJ 1;
			AKCK K 1 {
				A_ZoomFactor(0.995);
				A_TakeInventory("Kicking",1);
			}
			AKCK LM 1;
			AKCK NO 1 A_SetRoll(roll+2, SPF_INTERPOLATE);
			TNT1 A 1 {
				A_SetRoll(roll+2, SPF_INTERPOLATE);
				A_ZoomFactor(1.0);
				A_Takeinventory("PB_LockScreenTilt",1);
				A_OverlayFlags(-999,PSPF_PLAYERTRANSLATED, false);
				PB_SetUsingKick(false);
				A_Overlay(-10, "FirstPersonLegsStand");
			}
			Stop;
		GrabEnemy:	
			TNT1 A 0 {
				A_ClearOverlays(-10,65);
				A_StartSound("weapons/fistwhoosh");
			}
			PKUP ABCDEF 1;
			TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower;
			Wait;
    }
}